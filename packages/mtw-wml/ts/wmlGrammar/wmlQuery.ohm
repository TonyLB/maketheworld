WMLQuerySelector {
    WMLQueryContents = MatchAncestry

    wmlLegalTag = "Asset" |
        "Story" |
        "Character" |
        "Name" |
        "Pronouns" |
        "OneCoolThing" |
        "Outfit" |
        "FirstImpression" |
        "Condition" |
        "Layer" |
        "Exit" |
        "Description" |
        "Room" |
        "Feature" |
        "Map"

    stringText = (~("<" | "{" | "\"") any | "\\<" | "\\{" | "\\\"")+

    legalProperties = "key" |
        "to" |
        "from"

    propertyEqualityFilter = whitespace* legalProperties whitespace* "=\"" stringText "\""

    propertyFilter = "[" propertyEqualityFilter "]"

    firstFilter = ":first"

    nthChildFilter = ":nthChild(" digit* ")"

    matchComponent = wmlLegalTag |
        propertyFilter |
        firstFilter |
        nthChildFilter

    matchPredicate = matchComponent+

    MatchFlatItem = matchPredicate |
        MatchGroup

    MatchFlatList = MatchFlatItem+

    MatchGroup = "(" MatchAncestry ")"

    MatchOr = MatchFlatList MatchOrClause+

    MatchOrClause = "," MatchFlatList

    MatchAncestry = MatchOr |
        MatchFlatList

    whitespace = "\t"
        | "\x0B"    -- verticalTab
        | "\x0C"    -- formFeed
        | " "
        | "\u00A0"  -- noBreakSpace
        | "\uFEFF"  -- byteOrderMark

}